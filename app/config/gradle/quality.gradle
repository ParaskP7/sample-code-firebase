// LINT *********************************************************************************************************************

android { // Lint checks. See https://developer.android.com/studio/write/lint.html
    lintOptions {
        abortOnError true
        checkAllWarnings true
        ignoreWarnings false
        checkReleaseBuilds true
        warningsAsErrors true
        lintConfig file("config/lint/lint.xml")
        htmlReport true
        xmlReport true
    }
}

// FINDBUGS *****************************************************************************************************************

apply plugin: 'findbugs' // Findbugs plugin details: http://findbugs.sourceforge.net

findbugs {
    effort 'max'
    ignoreFailures false
    excludeFilter file("$rootProject.projectDir/app/config/static_analysis/findbugs/findbugs-exclusions.xml")
    reportLevel 'low'
    toolVersion versions.findbugsPlugin
    tasks.withType(FindBugs) {
        reports {
            xml.enabled true
            html.enabled false
        }
    }
}

task findBugs(type: FindBugs) {
    source = ['src/main/java/', 'src/test/java/']
    classes = files("$project.buildDir/intermediates/classes")
    classpath = files()
}


// PMD **********************************************************************************************************************


apply plugin: 'pmd' // Pmd plugin details: https://docs.gradle.org/current/userguide/pmd_plugin.html

pmd {
    consoleOutput true
    ignoreFailures false
    rulePriority 5 // Strictest mode available
    ruleSetConfig resources.text.fromFile("$rootProject.projectDir/app/config/static_analysis/pmd/pmd_rules.xml")
    toolVersion versions.pmdPlugin
    tasks.withType(Pmd) {
        reports {
            xml.enabled true
            html.enabled true
        }
    }
}

task pmd(type: Pmd) {
    source = ['src/main/java/', 'src/test/java/']
}
