apply plugin: 'com.android.application'
apply plugin: 'com.github.ben-manes.versions'

apply from: 'config/gradle/quality.gradle'

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

android {

    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        applicationId "com.hubrickchallenge.android"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode app.major * 10000 + app.minor * 1000 + app.patch * 100 + app.build
        versionName "${app.major}.${app.minor}.${app.patch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {

    compile deps.support.appCompat
    compile deps.support.constraintLayout
    compile deps.support.design
    compile deps.firebase.database
    compile deps.other.findbugs

    testCompile deps.test.junit

    androidTestCompile(deps.androidTest.espresso, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

}

apply plugin: 'com.google.gms.google-services'
