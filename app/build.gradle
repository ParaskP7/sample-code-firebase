apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'com.github.ben-manes.versions'

apply from: 'config/gradle/quality.gradle'
apply from: 'config/gradle/jacoco.gradle'

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

android {

    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        applicationId "com.hubrickchallenge.android"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode app.major * 10000 + app.minor * 1000 + app.patch * 100 + app.build
        versionName "${app.major}.${app.minor}.${app.patch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true // For vector image support.
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        pickFirst 'META-INF/rxjava.properties'
    }

    compileOptions {
        encoding 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {

    implementation deps.support.appCompat
    implementation deps.support.constraintLayout
    implementation deps.support.design
    implementation deps.support.cardView
    implementation deps.support.multiDex
    implementation deps.mosby.mvp
    implementation deps.mosby.viewState
    implementation deps.mosby.mvpNullObjectPresenter
    implementation deps.firebase.database
    implementation deps.dagger.runtime
    annotationProcessor deps.dagger.compiler
    implementation deps.butterKnife.runtime
    annotationProcessor deps.butterKnife.compiler
    implementation deps.rx.java
    implementation deps.rx.android
    implementation deps.rx.firebase
    implementation deps.event.bus
    implementation deps.logging.timber
    implementation deps.image.glide
    implementation deps.animation.lottie
    implementation deps.other.jodaTime
    implementation(deps.other.findbugs, {
        exclude group: 'net.jcip', module: 'jcip-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })

    testImplementation deps.test.junit

    androidTestImplementation(deps.androidTest.espresso, {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs'
    })

}

apply plugin: 'com.google.gms.google-services'
