apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'com.github.ben-manes.versions'

apply from: 'config/gradle/quality.gradle'
apply from: 'config/gradle/jacoco.gradle'

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

android {

    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        applicationId "com.hubrickchallenge.android"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode app.major * 10000 + app.minor * 1000 + app.patch * 100 + app.build
        versionName "${app.major}.${app.minor}.${app.patch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        pickFirst 'META-INF/rxjava.properties'
    }

}

dependencies {

    compile deps.support.appCompat
    compile deps.support.constraintLayout
    compile deps.support.design
    compile deps.support.cardView
    compile deps.support.multiDex
    compile deps.mosby.mvp
    compile deps.mosby.viewState
    compile deps.mosby.mvpNullObjectPresenter
    compile deps.firebase.database
    compile deps.dagger.runtime
    annotationProcessor deps.dagger.compiler
    compile deps.butterKnife.runtime
    annotationProcessor deps.butterKnife.compiler
    compile deps.rx.java
    compile deps.rx.android
    compile deps.rx.firebase
    compile deps.event.bus
    compile deps.logging.timber
    compile deps.animation.lottie
    compile deps.other.jodaTime
    compile(deps.other.findbugs, {
        exclude group: 'net.jcip', module: 'jcip-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })

    testCompile deps.test.junit

    androidTestCompile(deps.androidTest.espresso, {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs'
    })

}

apply plugin: 'com.google.gms.google-services'
